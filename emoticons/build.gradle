apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//读取local.properties
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion build_versions.compile_sdk

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly deps.appcompat
}

//上传版本
def groupId = 'com.xingxin.android'
def artifactId = 'emoticons'
def publishVersion = '1.1.3'
def repoName = 'maven'
def gitUrl = 'https://github.com/ingxin'
def website = 'https://www.jianshu.com/u/337d686feac1'
def descStr = 'A emoticons for Android'
def userOrg = 'ingxin'
def userEmail = 'xyin123@126.com'
def bintrayUser = properties.getProperty("bintray.user")
def bintrayKey = properties.getProperty("bintray.apikey")

version = publishVersion
group = groupId
bintray {
    user = bintrayUser
    key = bintrayKey
    pkg {
        repo = repoName
        name = artifactId
        websiteUrl = website
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        publish = true
        version {
            name = publishVersion
            desc = descStr
            released = new Date()
            vcsTag = 'v' + publishVersion
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
    configurations = ['archives']
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name descStr
                description descStr
                url website
                licenses {
                    license {
                        //和之前自己定义的协议一致
                        name 'Apache-2.0'
                        url 'https://www.apache.org/licenses/LICENSE-2.0.html'
                    }
                }
                developers {
                    developer {
                        //填写bintray或者github的用户名
                        id userOrg
                        //姓名
                        name userOrg
                        //邮箱
                        email userEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url website
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    //必须添加以免出错
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

javadoc {
    options {
        //如果你的项目里面有中文注释的话，必须将格式设置为UTF-8，不然会出现乱码
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}
